buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}

plugins {
    id "no.nils.wsdl2java" version "0.6"
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

description = 'LazyLogin-client'
version = '1.4'

configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
    generated {
        java { srcDirs = [
                "$projectDir/src/main/generated"
        ] }
        compileClasspath += sourceSets.main.output
    }
}

dependencies {
    compile group: 'commons-lang', name: 'commons-lang', version:'2.4'
    compile group: 'org.apache.cxf.xjc-utils', name: 'cxf-xjc-runtime', version:'3.0.0'
}

wsdl2java {
    generatedWsdlDir = file("$projectDir/src/main/generated")  // target directory for generated source coude
    wsdlsToGenerate = [   //  2d-array of wsdls and cxf-parameters
                          ['-xjc-Xts', '-p', 'com.lazylogin.client.system.v0',"$projectDir/src/main/resources/wsdl/system.wsdl"],
                          ['-xjc-Xts','-p', 'com.lazylogin.client.user.v0',"$projectDir/src/main/resources/wsdl/user.wsdl"]
    ]
    wsdlDir = file("$projectDir/src/main/resources/wsdl")
    cxfVersion = "3.0.0"
}

bintray {
    user = repositoryUser
    key = repositoryPass
    publications = ['jarBuild']

    publish = true

    pkg {
        repo = 'maven'
        name = "lazylogin"
        desc = project.description
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/albertonavarro/lazylogin'
        labels = ["api"]
    }
}

publish.dependsOn bintrayUpload

publishing {
    publications {
        jarBuild(MavenPublication) {
            from components.java

            artifactId "client"
            groupId project.group
            version project.version
        }
    }
}